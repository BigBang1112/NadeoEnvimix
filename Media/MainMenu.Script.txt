#RequireContext CManiaAppTitle

#Include "TextLib" as TextLib

#Include "Libs/BigBang1112/Random.Script.txt" as Random

declare CUILayer MainMenuLayer;

Void StartMenu() {
	declare RequestMainMenuPage = Http.CreateGet("file://Media/Manialinks/Menu.xml");
	wait(RequestMainMenuPage.IsCompleted);
	
	MainMenuLayer = UILayerCreate();
	MainMenuLayer.ManialinkPage = RequestMainMenuPage.Result;

	Http.Destroy(RequestMainMenuPage);
}

main() {	
	declare ProceedLayer = UILayerCreate();

	declare Text[] Zones = TextLib::Split("|", LocalUser.ZonePath);
	if(Zones.count == 0) {
		StartMenu();
	}
	else {
		declare RequestPlayer = Http.CreateGet("http://trackmania.bigbang1112.eu/nadeoenvimix/api/player.php?login="^LocalUser.Login);
		wait(RequestPlayer.IsCompleted);
		if(RequestPlayer.StatusCode == 200) {
			if(Xml.Create(RequestPlayer.Result).Root.GetFirstChild("Exists").TextContents == "0") {
				declare NonPredictableRandomString = Random::String(30,50);
		
				OpenLink("https://v4.live.maniaplanet.com/login/oauth2/authorize?response_type=code&scope=basic&state="^NonPredictableRandomString^"&client_id=d7de0ddcdc&redirect_uri=http:%2F%2Ftrackmania.bigbang1112.eu/nadeoenvimix/api/join.php", CManiaApp::ELinkType::ManialinkBrowser);
				
				declare RequestProceedPage = Http.CreateGet("file://Media/Manialinks/Proceed.xml");
				wait(RequestProceedPage.IsCompleted);
				ProceedLayer.ManialinkPage = RequestProceedPage.Result;
				Http.Destroy(RequestProceedPage);
			}
			else {
				StartMenu();
			}
		}
		else {
			StartMenu();
		}
		
		TitleFlow.GetServerInfo("nadeo_envimix");
	}
	
	while(True) {
		yield;
		
		foreach(Event,PendingEvents) {
			if(Event.Type == CManiaAppEvent::EType::LayerCustomEvent) {
				if(ProceedLayer != Null && Event.CustomEventLayer == ProceedLayer) {
					if(Event.CustomEventType == "PROCEED") {
						declare RequestPlayer = Http.CreateGet("http://trackmania.bigbang1112.eu/nadeoenvimix/api/player.php?login="^LocalUser.Login);
						wait(RequestPlayer.IsCompleted);
						if(RequestPlayer.StatusCode == 200) {
							if(Xml.Create(RequestPlayer.Result).Root.GetFirstChild("Exists").TextContents == "1") {
								ProceedLayer.IsVisible = False;
								StartMenu();
							}
							else {
								Menu_Quit();
							}
						}
						else {
							Menu_Quit();
						}
					}
				}
				else if(Event.CustomEventLayer == MainMenuLayer) {
					switch(Event.CustomEventType) {
						
						case "COMMAND_EXIT": {
							Menu_Quit();
						}
						case "COMMAND_PLAYMAP": {
							//TitleFlow.PlayMap(Event.CustomEventData[0] ^ "/" ^ Event.CustomEventData[1] ^ ".Map.Gbx","","");
							
							declare DifficultyNumber = TextLib::ToInteger(Event.CustomEventData[1]);
							declare MapNumber = TextLib::ToInteger(Event.CustomEventData[2]) - 1;
							
							declare CCampaign Campaign = Null;
							
							switch(Event.CustomEventData[0]) {
								case "Canyon": { Campaign <=> DataFileMgr.Campaigns[0]; }
								case "Stadium": { Campaign <=> DataFileMgr.Campaigns[1]; }
								case "Valley": { Campaign <=> DataFileMgr.Campaigns[2]; }
								case "Lagoon": { Campaign <=> DataFileMgr.Campaigns[3]; }
							}
							
							TitleFlow.PlayCampaign(Campaign,Campaign.MapGroups[DifficultyNumber].MapInfos[MapNumber],"","""
								<mode_script_settings>
									<setting name='S_Car' value='{{{Event.CustomEventData[3]}}}' type='text' />
								</mode_script_settings>
							""");
						}
						case "OPEN_ITEMEDITOR": {
							TitleFlow.OpenEditor(CTitleFlow::EEditorType::ItemEditor);
						}
						case "PLAY_POUTREL_SERVER": {
							TitleFlow.JoinServer("nadeo_envimix", False, "");
						}
					}
				}
			}
		}
	}
	
	UILayerDestroyAll();
}