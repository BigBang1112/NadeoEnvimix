#RequireContext CManiaAppTitle

#Include "TextLib" as TextLib

#Include "Libs/BigBang1112/Random.Script.txt" as Random

declare CUILayer MainMenuLayer;

Void StartMenu() {
	declare Music = Audio.CreateSound("file://Media/Musics/Menu/Silence.ogg", 1.0, True, True, False);
	Music.Play();
	
	MainMenuLayer = UILayerCreate();
	MainMenuLayer.ManialinkPage = "file://Media/Manialinks/Menu.xml";
	
	wait(TitleFlow.IsReady);
	TitleFlow.GetServerInfo("nadeo_envimix");
}

main() {	
	declare ProceedLayer = UILayerCreate();
	
	declare CServerInfo LastServerResult = Null;

	declare Text[] Zones = TextLib::Split("|", LocalUser.ZonePath);
	if(Zones.count == 0) {
		StartMenu();
	}
	else {
		declare RequestPlayer = Http.CreateGet("http://trackmania.bigbang1112.eu/nadeoenvimix/api/player.php?login="^LocalUser.Login);
		wait(RequestPlayer.IsCompleted);
		if(RequestPlayer.StatusCode == 200) {
			if(Xml.Create(RequestPlayer.Result).Root.GetFirstChild("Exists").TextContents == "0") {
				declare NonPredictableRandomString = Random::String(30,50);
		
				OpenLink("https://v4.live.maniaplanet.com/login/oauth2/authorize?response_type=code&scope=basic&state="^NonPredictableRandomString^"&client_id=d7de0ddcdc&redirect_uri=http:%2F%2Ftrackmania.bigbang1112.eu/nadeoenvimix/api/join.php", CManiaApp::ELinkType::ManialinkBrowser);
				
				ProceedLayer.ManialinkPage = "file://Media/Manialinks/Proceed.xml";
			}
			else {
				StartMenu();
			}
		}
		else {
			StartMenu();
		}
		
		log(ScoreMgr.ScoreStatus_GetMasterServerStatus(UserMgr.MainUser.Id));
	}
	
	declare LoadingLayer = UILayerCreate();
	LoadingLayer.ManialinkPage = "file://Media/Manialinks/Loading.xml";
	LoadingLayer.Type = CUILayer::EUILayerType::LoadingScreen; 
	
	while(True) {
		yield;
		
		if(TitleFlow.GetServerInfo_Result != Null && TitleFlow.GetServerInfo_Result != LastServerResult) {
			log("WHOP RESULT");
			LayerCustomEvent(MainMenuLayer, "SERVER_INFO", ["ONLINE"]);
			LastServerResult = TitleFlow.GetServerInfo_Result;
		}
		
		foreach(Event,PendingEvents) {
			if(Event.Type == CManiaAppEvent::EType::LayerCustomEvent) {
				if(ProceedLayer != Null && Event.CustomEventLayer == ProceedLayer) {
					if(Event.CustomEventType == "PROCEED") {
						declare RequestPlayer = Http.CreateGet("http://trackmania.bigbang1112.eu/nadeoenvimix/api/player.php?login="^LocalUser.Login);
						wait(RequestPlayer.IsCompleted);
						if(RequestPlayer.StatusCode == 200) {
							if(Xml.Create(RequestPlayer.Result).Root.GetFirstChild("Exists").TextContents == "1") {
								ProceedLayer.IsVisible = False;
								StartMenu();
							}
							else {
								Menu_Quit();
							}
						}
						else {
							Menu_Quit();
						}
					}
				}
				else if(Event.CustomEventLayer == MainMenuLayer) {
					switch(Event.CustomEventType) {
						
						case "COMMAND_EXIT": {
							Menu_Quit();
						}
						case "COMMAND_PLAYMAP": {
							//TitleFlow.PlayMap(Event.CustomEventData[0] ^ "/" ^ Event.CustomEventData[1] ^ ".Map.Gbx","","");
							
							declare DifficultyNumber = TextLib::ToInteger(Event.CustomEventData[1]);
							declare MapNumber = TextLib::ToInteger(Event.CustomEventData[2]) - 1;
							
							declare CCampaign Campaign = Null;
							
							switch(Event.CustomEventData[0]) {
								case "Canyon": { Campaign <=> DataFileMgr.Campaigns[0]; }
								case "Stadium": { Campaign <=> DataFileMgr.Campaigns[1]; }
								case "Valley": { Campaign <=> DataFileMgr.Campaigns[2]; }
								case "Lagoon": { Campaign <=> DataFileMgr.Campaigns[3]; }
							}
							
							TitleFlow.PlayCampaign(Campaign,Campaign.MapGroups[DifficultyNumber].MapInfos[MapNumber],"","""
								<mode_script_settings>
									<setting name='S_Car' value='{{{Event.CustomEventData[3]}}}' type='text' />
								</mode_script_settings>
							""");
						}
						case "OPEN_ITEMEDITOR": {
							TitleFlow.OpenEditor(CTitleFlow::EEditorType::ItemEditor);
						}
						case "PLAY_POUTREL_SERVER": {
							TitleFlow.JoinServer("nadeo_envimix", False, "");
						}
					}
				}
			}
		}
	}
	
	UILayerDestroyAll();
}